object String
    @type_checker "String"
	@c_type "char *"
	@c_byte_size "*"
	@java_type "String"
    @js_type "String"
    @SenML_type "String";
	
datatype Boolean<1>
    @type_checker "Boolean"
	@c_type "uint8_t"
	@c_byte_size "2"
	@java_type "boolean"
 	@js_type "boolean"
	@java_primitive "true";

thing fragment PingPongMsgs {
	
	message ping(ip: String);
	message pong();
	
	message query(client_ip: String);
	message prediction_positive();
	message prediction_negative();
	
}

thing PingClient includes PingPongMsgs {
	required port ping_service {
		sends ping
		receives pong
	}
		
	property my_ip_address: String = "192.168.1.1" 
	
	statechart PingClientBehavior init Ping {
	
	on entry print "Ping/Pong Client Started!\n"
	
		state Ping {
			
			on entry do				
				print "Sending Ping...\n"				
				ping_service!ping(my_ip_address)				
			end				
			
			transition -> Pong 
			event ping_service?pong
		}
		
		state Pong {
			on entry print "Got pong!\n"
			
			transition -> Stop
			
		}
		
		state Stop {
			on entry print "Bye.\n"
		}
		
	}

}

thing PingServer includes PingPongMsgs {
	
	provided port ping_service {
		receives ping
		sends pong
	}
	
	required port da_service {
		sends query
		receives prediction_positive, prediction_negative
	}
	
	property client_ip_address: String
	property malicious_client: Boolean
	
	statechart PongServerBehavior init GetPing {

        on entry print "Ping/Pong Server Started!\n"
	
		state GetPing {
			
			internal event e: ping_service?ping			
			action		 
			do				
			client_ip_address = e.ip
			print("Checking if the client is a malicious one...\n")
			da_service!query(client_ip_address)
			end
			transition -> Pong
			event da_service?prediction_negative
			
			transition -> Ignore
			event da_service?prediction_positive
		}
		
		state Pong {
			on entry do
			print "Got ping from: " + client_ip_address + "\n"
			print "Sending Pong...\n"
			ping_service!pong()
			end
			transition -> GetPing
		}
		
		state Ignore {
			on entry do
			print "Got ping from: " + client_ip_address + "\n"
			print "Ignoring the ping message, since the client is probably a malicious one...\n"
			end
			transition -> GetPing
		}
	}

}

thing PingPongDataAnalytics includes PingPongMsgs {
		
	provided port da_service {
		receives query
		sends prediction_positive, prediction_negative
	}
	
	property client_ip_address: String
	property prediction: Boolean = false

	data_analytics da1 {
		dataset "data/ip_dataset.csv"
		sequential TRUE
		timestamps ON
		labels ON
		features client_ip_address,prediction
		//model_algorithm decision_tree my_dt()
		//model_algorithm random_forest my_rf()
		model_algorithm nn_multilayer_perceptron my_nn_mlp(activation relu optimizer Adam loss sparse_categorical_crossentropy)
		training_results "data/training.txt"		
		prediction_results prediction
	}

	statechart PingPongDataAnalyticsBehavior init Preprocess {
	
	on entry print "Ping Pong Data Analytics Started!\n"
	
		state Preprocess {
			on entry do
			print "Ping Pong Data Analytics: Data Preprocessing\n"
			da_preprocess da1
			end
			transition -> Train			
		}
		
		state Train {
			on entry do
			print "Ping Pong Data Analytics: Training\n"
			da_train da1
			end
			transition -> Ready
		}
		
		state Ready {
			on entry do
			print "Ping Pong Data Analytics: Ready for Prediction\n"
			end
			transition -> Predict
			event m: da_service?query
			action client_ip_address = m.client_ip
		}
		
		state Predict {
			on entry do
			print "Ping Pong Data Analytics: Predicting\n"
			da_predict da1(client_ip_address)
			if(prediction==false)
			da_service!prediction_negative()
			else
			da_service!prediction_positive()		
			end
			transition -> Ready
			on exit da_save da1
		}	
	}
}

configuration SmartPingPongCfg {
		
    instance pingClient : PingClient
    instance pingServer : PingServer
    instance pingPongDataAnalytics : PingPongDataAnalytics

    connector pingClient.ping_service => pingServer.ping_service
    connector pingServer.da_service => pingPongDataAnalytics.da_service
    
}
